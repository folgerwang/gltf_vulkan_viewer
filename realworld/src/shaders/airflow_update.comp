#version 450
#extension GL_ARB_separate_shader_objects : enable
#include "global_definition.glsl.h"
#include "weather_common.glsl.h"
#include "tile_common.glsl.h"
#include "noise.glsl.h"

layout(push_constant) uniform AirflowUniformBufferObject {
  AirflowUpdateParams params;
};

layout(set = 0, binding = DST_TEMP_MOISTURE_TEX_INDEX, rg16) uniform writeonly image3D dst_temp_moisture;
layout(set = 0, binding = SRC_TEMP_MOISTURE_TEX_INDEX, rg16) uniform readonly image3D src_temp_moisture;
layout(set = 0, binding = DST_AIRFLOW_TEX_INDEX, rgba8) uniform image3D dst_airflow;
layout(set = 0, binding = ROCK_LAYER_BUFFER_INDEX) uniform sampler2D rock_layer;
layout(set = 0, binding = SOIL_WATER_LAYER_BUFFER_INDEX) uniform sampler2D soil_water_layer;

shared vec2 s_ground_info[12][12];
shared uint s_temp_moisture_grid[20][12][12];
shared uint s_flow_grid[18][10][10];

vec2 getPackedLayers(vec2 uv) {
    float rock_height = texture(rock_layer, uv).x;
    vec2 soil_water_thickness = 
        texture(soil_water_layer, uv).xy * SOIL_WATER_LAYER_MAX_THICKNESS;

    float soil_layer_height = rock_height + soil_water_thickness.x;
    float water_layer_height = soil_layer_height + soil_water_thickness.y;
    
    return vec2(soil_layer_height, water_layer_height);
}

uint packTempMoist(float temp, float moist, uint tag) {
    temp /= kMaxTemperatureAdjustRange;
    moist /= kMaxMoistureIntensity;
    uint packed_moist_temp = 
        packUnorm2x16(vec2(
            moist,
            temp * 0.5f + 0.5f));
    packed_moist_temp = (packed_moist_temp & 0xfffffff8) | (tag & 0x07);
    return packed_moist_temp;
}

vec2 unpackTempMoist(uint packed_temp_moist) {
    vec2 snorm_moist_temp = unpackUnorm2x16(packed_temp_moist & 0xfffffff8);
    return vec2((snorm_moist_temp.y * 2.0f - 1.0f) * kMaxTemperatureAdjustRange,
                snorm_moist_temp.x * kMaxMoistureIntensity);
}

uint packTempMoistDiff(float temp, float moist, uint tag) {
    uint temp_moist_diff = packUnorm2x16(vec2(moist, temp) / kMaxTempMoistDiff);
    temp_moist_diff = (temp_moist_diff & 0xfffffff8) | (tag & 0x07);
    return temp_moist_diff;
}

uint getUpdatedTemperature(ivec3 slc, ivec3 block_coords, vec2 ground_info) {
    ivec3 src_coords = clamp(slc + block_coords - 2,
                             ivec3(0),
                             params.size - 1);
    vec2 src_temp_moist = imageLoad(src_temp_moisture, src_coords).xy;
    float src_temp = denormalizeTemperature(src_temp_moist.x);
    float src_moist = denormalizeMoisture(src_temp_moist.y);

    float uvw_w = (src_coords.z + 0.5) * params.inv_size.z;
    float min_h = getSampleToHeight(uvw_w - params.inv_size.z * 0.5f);
    float max_h = getSampleToHeight(uvw_w + params.inv_size.z * 0.5f);
    float sample_h = getSampleToHeight(uvw_w);

    float target_stable_temp = getReferenceDegree(params.controls.sea_level_temperature, sample_h);

    float water_depth = ground_info.y - ground_info.x;
    bool cell_touch_ground = (ground_info.y >= min_h - 0.01f);
    bool cell_above_ground = ground_info.y < min_h;
    float temp_adjust = 0.0f;
    float moist_adjust = 0.0f;
    if (cell_touch_ground) {
        float water_level = clamp((water_depth - 0.01f) * 10.0f, 0.0f, 1.0f);
        temp_adjust += mix(params.controls.soil_temp_adj, params.controls.water_temp_adj, water_level);
        moist_adjust += mix(params.controls.soil_moist_adj, params.controls.water_moist_adj, water_level);
    }
    else if (cell_above_ground) {
        temp_adjust += params.controls.moist_temp_convert * src_moist;
    }

    vec4 noise_values = hash44(vec4(vec3(src_coords), params.current_time));

    temp_adjust *= (noise_values.x + noise_values.y);
    moist_adjust *= (noise_values.z + noise_values.w);

    // make sure color different here is positive.
    return packTempMoist(src_temp + temp_adjust - target_stable_temp,
                         src_moist + moist_adjust,
                         0);
}

void updateTempMoistureBuffer(int pixel_index, ivec3 block_coords) {
    int iz = pixel_index / (12 * 12);
    int ixy = pixel_index % (12 * 12);
    ivec3 slc = ivec3(ixy % 12, ixy / 12, iz);

    vec2 ground_info = s_ground_info[slc.y][slc.x];

    uint temp_moist = getUpdatedTemperature(slc, block_coords, ground_info);
    s_temp_moisture_grid[slc.z][slc.y][slc.x] = temp_moist;
}

uint packTempDiff(uint min_temp, uint min_moist, uint center, ivec3 pixel_coords) {
    vec2 min_tm = unpackTempMoist(min_temp);
    vec2 min_m = unpackTempMoist(min_moist);
    vec2 center_tm = unpackTempMoist(center);

    vec4 noise_values = hash44(vec4(vec3(pixel_coords), params.current_time)) * 2.0f - 1.0f;
    float uvw_w = (pixel_coords.z + 0.5) * params.inv_size.z;
    float sample_h = getSampleToHeight(uvw_w);

    float target_stable_temp = getReferenceDegree(params.controls.sea_level_temperature, sample_h);

    float temp_transfer = params.controls.heat_transfer_ratio * (1.0f + noise_values.x * params.controls.heat_transfer_noise_weight);
    float moist_transfer = params.controls.moist_transfer_ratio * (1.0f + noise_values.y * params.controls.moist_transfer_noise_weight);

    float frozen_transfer_rate = (target_stable_temp - 15.0f) / 5.0f;
    float transfer_frozen = frozen_transfer_rate + noise_values.z * 0.3f;

    uint packed_temp_moist_diff = 0;
    if (center_tm.x > min_tm.x) {
        float temp_diff = center_tm.x - min_tm.x;
        float temp_diff_adj = temp_diff * temp_transfer;
        float moist_adj = center_tm.y * clamp(moist_transfer * transfer_frozen, 0.0f, 1.0f);
        packed_temp_moist_diff = packTempMoistDiff(temp_diff_adj, moist_adj, min_temp);
    }

    return packed_temp_moist_diff;
}

void createTempMoistureFlowBuffer(int pixel_index, ivec3 block_coords) {
    int iz = pixel_index / (10 * 10);
    int ixy = pixel_index % (10 * 10);

    ivec3 dlc = ivec3(ixy % 10, ixy / 10, iz);
    ivec3 slc = dlc + 1;

    uint center = s_temp_moisture_grid[slc.z][slc.y][slc.x];

    uint left = s_temp_moisture_grid[slc.z][slc.y][slc.x-1] | 0x00;
    uint right = s_temp_moisture_grid[slc.z][slc.y][slc.x+1] | 0x01;
    uint back = s_temp_moisture_grid[slc.z][slc.y-1][slc.x] | 0x02;
    uint front = s_temp_moisture_grid[slc.z][slc.y+1][slc.x] | 0x03;
    uint top = s_temp_moisture_grid[slc.z-1][slc.y][slc.x] | 0x04;
    uint bottom = s_temp_moisture_grid[slc.z+1][slc.y][slc.x] | 0x05;

    uint min_temp = min(min(min(left, right), min(back, front)), min(top, bottom));

    left &= 0xffff;
    right &= 0xffff;
    back &= 0xffff;
    front &= 0xffff;
    top &= 0xffff;
    bottom &= 0xffff;

    uint min_moist = min(min(min(left, right), min(back, front)), min(top, bottom));

    s_flow_grid[dlc.z][dlc.y][dlc.x] = packTempDiff(min_temp, min_moist, center, dlc + block_coords);
}

vec2 generateAirflow(ivec3 dlc, out vec3 airflow_dir, float above_ratio, float below_ratio) {
    ivec3 slc = dlc + 1;
    uint center = s_flow_grid[slc.z][slc.y][slc.x];
    uint back = s_flow_grid[slc.z][slc.y-1][slc.x];
    uint front = s_flow_grid[slc.z][slc.y+1][slc.x];
    uint top = s_flow_grid[slc.z-1][slc.y][slc.x];
    uint bottom = s_flow_grid[slc.z+1][slc.y][slc.x];
    uint left = s_flow_grid[slc.z][slc.y][slc.x-1];
    uint right = s_flow_grid[slc.z][slc.y][slc.x+1];

    uvec2 temp_moist_adj = ivec2(0);
    ivec3 airflow_vec = ivec3(0);
    if ((left & 0x07) == 1) {
        uint temp_diff = left >> 16;
        temp_moist_adj.x += temp_diff;
        temp_moist_adj.y += left & 0xfff8;
        airflow_vec += ivec3(temp_diff, 0, 0);
    }
    if ((right & 0x07) == 0) {
        uint temp_diff = right >> 16;
        temp_moist_adj.x += temp_diff;
        temp_moist_adj.y += right & 0xfff8;
        airflow_vec += ivec3(-temp_diff, 0, 0);
    }
    if ((back & 0x07) == 3) {
        uint temp_diff = back >> 16;
        temp_moist_adj.x += temp_diff;
        temp_moist_adj.y += back & 0xfff8;
        airflow_vec += ivec3(0, 0, temp_diff);
    }
    if ((front & 0x07) == 2) {
        uint temp_diff = front >> 16;
        temp_moist_adj.x += temp_diff;
        temp_moist_adj.y += front & 0xfff8;
        airflow_vec += ivec3(0, 0, -temp_diff);
    }
    if ((top & 0x07) == 5) {
        uint temp_diff = uint(float(top >> 16) * above_ratio);
        temp_moist_adj.x += temp_diff;
        temp_moist_adj.y += uint(float(top & 0xfff8) * above_ratio);
        airflow_vec += ivec3(0, temp_diff, 0);
    }
    if ((bottom & 0x07) == 4) {
        uint temp_diff = uint(float(bottom >> 16) * below_ratio);
        temp_moist_adj.x += temp_diff;
        temp_moist_adj.y += uint(float(bottom & 0xfff8) * below_ratio);
        airflow_vec += ivec3(0, -temp_diff, 0);
    }

    uint flow_tag = center & 0x07;
    uint temp_diff = center >> 16;
    uint moist_diff = center & 0xfff8;

    airflow_vec += ivec3(flow_tag == 0 ? -temp_diff : (flow_tag == 1 ? temp_diff : 0),
                        flow_tag == 4 ? -temp_diff : (flow_tag == 5 ? temp_diff : 0),
                        flow_tag == 2 ? -temp_diff : (flow_tag == 3 ? temp_diff : 0));

    airflow_dir = vec3(airflow_vec) * kTempMoistDiffToFloat;
    return (ivec2(temp_moist_adj) - ivec2(temp_diff, moist_diff)) * kTempMoistDiffToFloat;
}

layout(local_size_x = 8, local_size_y = 8, local_size_z = 16) in;
void main()
{
    ivec3 pixel_coords = ivec3(gl_GlobalInvocationID.xyz);
    ivec3 local_coords = ivec3(gl_LocalInvocationID.xyz);
    ivec3 block_coords = ivec3(gl_WorkGroupID.xyz) * ivec3(8, 8, 16);
    int pixel_index = int(gl_LocalInvocationIndex);

    if (pixel_index < 12 * 12) {
        ivec2 slc = ivec2(pixel_index % 12, pixel_index / 12);
        ivec2 src_coords = clamp(slc + block_coords.xy - 2,
                                 ivec2(0),
                                 params.size.xy - 1);
        s_ground_info[slc.y][slc.x] =
            getPackedLayers((src_coords + 0.5) * params.inv_size.xy);
    }

    barrier();
    memoryBarrierShared();

    if (pixel_index < (20 * 12 * 12 / 3)) {
        for (int i = 0; i < 3; i++) {
            updateTempMoistureBuffer(pixel_index * 3 + i, block_coords);
        }
    }

    barrier();
    memoryBarrierShared();

    if (pixel_index < (18 * 10 * 10 / 2)) {
        for (int i = 0; i < 2; i++) {
            createTempMoistureFlowBuffer(pixel_index * 2 + i, block_coords);
        }
    }

    barrier();
    memoryBarrierShared();

    vec3 airflow_dir;
    float uvw_w = (pixel_coords.z + 0.5) * params.inv_size.z;
    float sample_delta_h = getSampleToDeltaHeight(uvw_w);
    float above_sample_delta_h = getSampleToDeltaHeight(uvw_w + params.inv_size.z);
    float below_sample_delta_h = getSampleToDeltaHeight(uvw_w - params.inv_size.z);
    vec2 temp_moist_adj = generateAirflow(local_coords,
                                          airflow_dir,
                                          above_sample_delta_h / sample_delta_h,
                                          below_sample_delta_h / sample_delta_h);

    float sample_h = getSampleToHeight(uvw_w);
    float target_stable_temp = getReferenceDegree(params.controls.sea_level_temperature, sample_h);

    ivec3 slc = local_coords + 2;
    vec2 temp_moist = unpackTempMoist(s_temp_moisture_grid[slc.z][slc.y][slc.x]);

    vec2 src_temp_moist = imageLoad(src_temp_moisture, pixel_coords).xy;
    float src_temp = denormalizeTemperature(src_temp_moist.x);
    float src_moist = denormalizeMoisture(src_temp_moist.y);

    float cur_temp = target_stable_temp + temp_moist.x + temp_moist_adj.x;
    float cur_moist = temp_moist.y + temp_moist_adj.y;

    cur_temp = mix(cur_temp, src_temp, 0.0f);
    cur_moist = mix(cur_moist, src_moist, 0.8f);
    imageStore(dst_temp_moisture,
               pixel_coords,
               vec4(normalizeTemperature(cur_temp), normalizeMoisture(cur_moist), 0, 0));

    vec4 src_airflow = imageLoad(dst_airflow, pixel_coords);
    vec3 src_airflow_dir = src_airflow.xyz * 2.0f - 1.0f;
    airflow_dir = mix(airflow_dir, src_airflow_dir * getPackedVectorLength(src_airflow.w), params.controls.mix_rate);
    float new_airflow_str = getNormalizedVectorLength(airflow_dir);
    imageStore(dst_airflow, pixel_coords, vec4(normalize(airflow_dir) * 0.5f + 0.5f, new_airflow_str));
}